plugins {
    id("java-library")
    id("com.vanniktech.maven.publish") version "0.36.0"
}

group = "dev.musca"
version = System.getenv("GITHUB_REF_NAME")?.removePrefix("v") ?: "0.0.1-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
	api("dev.musca:fluff-core:2.0.0")
    api("dev.musca:fluff-functions:2.0.0")
}

tasks.withType<Jar> {
    from(layout.projectDirectory) {
        include("LICENSE", "NOTICE")
        into("META-INF")
    }
}

mavenPublishing {
    publishToMavenCentral(automaticRelease = true)
    signAllPublications()
    coordinates(project.group.toString(), project.name, project.version.toString())

    val developerId = "muscaa"
    val developerName = "musca"
    val projectId = project.name

    pom {
        name.set("Fluff Sorting")
        description.set("A library for sorting lists, arrays, and data structures using pre-implemented or custom algorithms")
        inceptionYear.set("2024")
        url.set("https://github.com/${developerId}/${projectId}/")
        licenses {
            license {
                name.set("The Apache License, Version 2.0")
                url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                distribution.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
            }
        }
        developers {
            developer {
                id.set(developerId)
                name.set(developerName)
                url.set("https://github.com/${developerId}/")
            }
        }
        scm {
            url.set("https://github.com/${developerId}/${projectId}/")
            connection.set("scm:git:git://github.com/${developerId}/${projectId}.git")
            developerConnection.set("scm:git:ssh://git@github.com/${developerId}/${projectId}.git")
        }
    }
}
